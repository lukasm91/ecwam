! (C) Copyright 1989- ECMWF.
! 
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.
!

SUBROUTINE WAMINTGR_LOKI_GPU(CDTPRA, CDATE, CDATEWH, CDTIMP, CDTIMPNEXT,  &
 &                   BLK2GLO,                                     &
 &                   WVENVI, WVPRPT, FF_NOW, FF_NEXT, INTFLDS,    &
 &                   WAM2NEMO, MIJ, FL1, XLLWS)


! ----------------------------------------------------------------------

!**** *WAMINTGR* - 3-G WAM MODEL - TIME INTEGRATION OF WAVE FIELDS.

!*    PURPOSE.
!     --------

!       COMPUTATION OF THE 2-D FREQUENCY-DIRECTION WAVE SPECTRUM AT ALL
!       GRID POINTS FOR A GIVEN INITIAL SPECTRUM AND FORCING SURFACE
!       STRESS FIELD.

!     REFERENCE.
!     ----------

!         IFS DOCUMENTATION, part VII 

! -------------------------------------------------------------------

USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU, JWRO
USE YOWDRVTYPE  , ONLY : WVGRIDGLO, ENVIRONMENT, FREQUENCY, FORCING_FIELDS,  &
 &                       INTGT_PARAM_FIELDS, WAVE2OCEAN

USE YOWCOUP  , ONLY : LWNEMOCOU, NEMONTAU
USE YOWGRID  , ONLY : NPROMA_WAM, NCHNK
USE YOWPARAM , ONLY : NIBLO, NANG, NFRE
USE YOWPCONS , ONLY : EPSMIN
USE YOWSTAT  , ONLY : CDTPRO, IDELPRO, IDELT, IDELWI, LLSOURCE, TIME_PROPAG, TIME_PHYS, &
 &  TIME_PHYS_KERNEL
USE YOWWIND  , ONLY : CDAWIFL, CDATEWO, CDATEFL
USE YOWFIELD_MOD, ONLY : FREQUENCY_FIELD, ENVIRONMENT_FIELD, FORCING_FIELDS_FIELD, &
 &  WAVE2OCEAN_FIELD, INTGT_PARAM_FIELDS_FIELD, SOURCE_CONTRIBS_FIELD

USE YOMHOOK  , ONLY : LHOOK, DR_HOOK, JPHOOK
USE NVTX

! ----------------------------------------------------------------------

IMPLICIT NONE

#include "implsch.intfb.h"
#include "snonlin.intfb.h"
#include "incdate.intfb.h"
#include "newwind.intfb.h"
#include "propag_wam.intfb.h"
#include "sinflx.intfb.h"
#include "wam_user_clock.intfb.h"

CHARACTER(LEN=14), INTENT(IN)                                            :: CDTPRA  ! DATE FOR CALL PROPAGATION
CHARACTER(LEN=14), INTENT(INOUT)                                         :: CDATE  ! CURRENT DATE
CHARACTER(LEN=14), INTENT(INOUT)                                         :: CDATEWH ! DATE OF THE NEXT FORCING FIELDS
CHARACTER(LEN=14), INTENT(INOUT)                                         :: CDTIMP  ! START DATE OF SOURCE FUNCTION INTEGRATION
CHARACTER(LEN=14), INTENT(INOUT)                                         :: CDTIMPNEXT  ! NEXT START DATE OF SOURCE FUNCTION INTEGRATION
TYPE(WVGRIDGLO), INTENT(IN)                                              :: BLK2GLO  ! BLOCK TO GRID TRANSFORMATION
TYPE(ENVIRONMENT), INTENT(INOUT)                                         :: WVENVI !  WAVE ENVIRONMENT FIELDS
TYPE(FREQUENCY), INTENT(INOUT)                                           :: WVPRPT  ! WAVE PROPERTIES FIELDS
TYPE(FORCING_FIELDS), INTENT(INOUT)                                      :: FF_NOW  ! FORCING FIELDS AT CURRENT TIME
TYPE(FORCING_FIELDS), INTENT(IN)                                         :: FF_NEXT  !  DATA STRUCTURE WITH THE NEXT FORCING FIELDS
TYPE(INTGT_PARAM_FIELDS), INTENT(INOUT)                                  :: INTFLDS  ! INTEGRATED/DERIVED PARAMETERS
TYPE(WAVE2OCEAN), INTENT(INOUT)                                          :: WAM2NEMO  ! WAVE FIELDS PASSED TO NEMO
INTEGER(KIND=JWIM), DIMENSION(NPROMA_WAM, NCHNK), INTENT(INOUT)          :: MIJ  ! LAST FREQUENCY INDEX OF THE PROGNOSTIC RANGE
REAL(KIND=JWRB), DIMENSION(NPROMA_WAM, NANG, NFRE, NCHNK), INTENT(INOUT) :: FL1
REAL(KIND=JWRB), DIMENSION(NPROMA_WAM, NANG, NFRE, NCHNK), INTENT(INOUT) :: XLLWS ! TOTAL WINDSEA MASK FROM INPUT SOURCE TERM

REAL(KIND=JWRB) :: TIME0, TIME2


INTEGER(KIND=JWIM) :: IJ, K, M
INTEGER(KIND=JWIM) :: ICHNK
INTEGER(KIND=JWIM) :: IDELWH

! Objects to store fields
TYPE(FREQUENCY_FIELD) :: WVPRPT_FIELD
TYPE(ENVIRONMENT_FIELD) :: WVENVI_FIELD
TYPE(FORCING_FIELDS_FIELD) :: FF_NOW_FIELD
TYPE(WAVE2OCEAN_FIELD) :: WAM2NEMO_FIELD
TYPE(INTGT_PARAM_FIELDS_FIELD) :: INTFLDS_FIELD
TYPE(SOURCE_CONTRIBS_FIELD) :: SRC_CONTRIBS

! DEVICE POINTERS
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: FL1_DPTR(:,:,:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: XLLWS_DPTR(:,:,:,:) => NULL()
INTEGER(KIND=JWIM), POINTER, CONTIGUOUS :: MIJ_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: WAVNUM_DPTR(:,:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: CGROUP_DPTR(:,:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: CIWA_DPTR(:,:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: CINV_DPTR(:,:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: XK2CG_DPTR(:,:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: STOKFAC_DPTR(:,:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: EMAXDPT_DPTR(:,:) => NULL()
INTEGER(KIND=JWIM), POINTER, CONTIGUOUS :: INDEP_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: DEPTH_DPTR(:,:) => NULL()
INTEGER(KIND=JWIM), POINTER, CONTIGUOUS :: IOBND_DPTR(:,:) => NULL()
INTEGER(KIND=JWIM), POINTER, CONTIGUOUS :: IODP_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: CICOVER_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: WSWAVE_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: WDWAVE_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: AIRD_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: WSTAR_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: UFRIC_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: TAUW_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: TAUWDIR_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: Z0M_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: Z0B_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: CHRNCK_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: CITHICK_DPTR(:,:) => NULL()
REAL(KIND=JWRO), POINTER, CONTIGUOUS :: NEMOUSTOKES_DPTR(:,:) => NULL()
REAL(KIND=JWRO), POINTER, CONTIGUOUS :: NEMOVSTOKES_DPTR(:,:) => NULL()
REAL(KIND=JWRO), POINTER, CONTIGUOUS :: NEMOSTRN_DPTR(:,:) => NULL()
REAL(KIND=JWRO), POINTER, CONTIGUOUS :: NPHIEPS_DPTR(:,:) => NULL()
REAL(KIND=JWRO), POINTER, CONTIGUOUS :: NTAUOC_DPTR(:,:) => NULL()
REAL(KIND=JWRO), POINTER, CONTIGUOUS :: NSWH_DPTR(:,:) => NULL()
REAL(KIND=JWRO), POINTER, CONTIGUOUS :: NMWP_DPTR(:,:) => NULL()
REAL(KIND=JWRO), POINTER, CONTIGUOUS :: NEMOTAUX_DPTR(:,:) => NULL()
REAL(KIND=JWRO), POINTER, CONTIGUOUS :: NEMOTAUY_DPTR(:,:) => NULL()
REAL(KIND=JWRO), POINTER, CONTIGUOUS :: NEMOWSWAVE_DPTR(:,:) => NULL()
REAL(KIND=JWRO), POINTER, CONTIGUOUS :: NEMOPHIF_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: WSEMEAN_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: WSFMEAN_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: USTOKES_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: VSTOKES_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: STRNMS_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: TAUXD_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: TAUYD_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: TAUOCXD_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: TAUOCYD_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: TAUOC_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: PHIOCD_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: PHIEPS_DPTR(:,:) => NULL()
REAL(KIND=JWRB), POINTER, CONTIGUOUS :: PHIAW_DPTR(:,:) => NULL()

REAL(KIND=JWRB), DIMENSION(NPROMA_WAM,NCHNK) :: FMEANWS, EMEAN, FMEAN
REAL(KIND=JWRB), DIMENSION(NPROMA_WAM,NCHNK) :: F1MEAN, AKMEAN, XKMEAN
REAL(KIND=JWRB), DIMENSION(NPROMA_WAM,NCHNK) :: PHIWA
REAL(KIND=JWRB), DIMENSION(NPROMA_WAM,NCHNK) :: HALP
REAL(KIND=JWRB), DIMENSION(NPROMA_WAM,NANG,NCHNK) :: FLM
REAL(KIND=JWRB), DIMENSION(NPROMA_WAM,NANG,NCHNK) :: COSWDIF, SINWDIF2
REAL(KIND=JWRB), DIMENSION(NPROMA_WAM,NFRE,NCHNK) :: RHOWGDFTH
REAL(KIND=JWRB), DIMENSION(NPROMA_WAM,NANG,NFRE,NCHNK) :: FLD, SL, SPOS
REAL(KIND=JWRB), DIMENSION(NPROMA_WAM,NANG,NFRE,NCHNK) :: CIREDUC 
REAL(KIND=JWRB), DIMENSION(NPROMA_WAM,NANG,NFRE,NCHNK) :: SSOURCE 

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

LOGICAL :: LUPDTUS

LOGICAL, SAVE :: LLNEWFILE
INTEGER, SAVE :: INONLIN_IMPL = -1
CHARACTER(LEN=10) :: CNONLIN_IMPL
INTEGER(KIND=JWIM) :: ICALL, NCALL

DATA LLNEWFILE / .FALSE. /

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('WAMINTGR',0,ZHOOK_HANDLE)

IF (INONLIN_IMPL == -1) THEN
  CALL GET_ENVIRONMENT_VARIABLE("NVIDIA_NONLIN_IMPL", CNONLIN_IMPL)
  IF (TRIM(CNONLIN_IMPL) == "1") THEN
    INONLIN_IMPL = 1
  ELSE
    INONLIN_IMPL = 0
  ENDIF
ENDIF

!*     PROPAGATION TIME
!      ----------------

IF (CDATE == CDTPRA) THEN
  CALL nvtxStartRange("PROPAG")
  TIME0=-WAM_USER_CLOCK()
  CALL PROPAG_WAM(BLK2GLO, WVENVI, WVPRPT, FL1)
  TIME_PROPAG = TIME_PROPAG + (TIME0+WAM_USER_CLOCK())*1.E-06
  CDATE = CDTPRO
  CALL nvtxEndRange
ENDIF


!* RETRIEVING NEW FORCING FIELDS IF NEEDED.
!  ----------------------------------------
CALL nvtxStartRange("NEWWIND")
CALL NEWWIND(CDTIMP, CDATEWH, LLNEWFILE,      &
 &           WVPRPT, FF_NOW, FF_NEXT)
CALL nvtxEndRange

! IT IS TIME TO INTEGRATE THE SOURCE TERMS
! ----------------------------------------
IF (CDATE >= CDTIMPNEXT) THEN
! COMPUTE UPDATE DUE TO SOURCE TERMS
  CALL GSTATS(1431,0)
  IF (LLSOURCE) THEN

    TIME2=-WAM_USER_CLOCK()
    CALL nvtxStartRange("IMPLSCH:INIT")
    CALL WVPRPT_FIELD%INIT(WAVNUM=WVPRPT%WAVNUM, CGROUP=WVPRPT%CGROUP, CIWA=WVPRPT%CIWA, CINV=WVPRPT%CINV, XK2CG=WVPRPT%XK2CG, &
 &                         STOKFAC=WVPRPT%STOKFAC)
    CALL WVENVI_FIELD%INIT(EMAXDPT=WVENVI%EMAXDPT, INDEP=WVENVI%INDEP, DEPTH=WVENVI%DEPTH, IOBND=WVENVI%IOBND, IODP=WVENVI%IODP)
    CALL FF_NOW_FIELD%INIT(AIRD=FF_NOW%AIRD, WDWAVE=FF_NOW%WDWAVE, CICOVER=FF_NOW%CICOVER, WSWAVE=FF_NOW%WSWAVE, &
&                          WSTAR=FF_NOW%WSTAR, UFRIC=FF_NOW%UFRIC, TAUW=FF_NOW%TAUW, TAUWDIR=FF_NOW%TAUWDIR, &
&                          Z0M=FF_NOW%Z0M, Z0B=FF_NOW%Z0B, CHRNCK=FF_NOW%CHRNCK, CITHICK=FF_NOW%CITHICK)
    CALL WAM2NEMO_FIELD%INIT(NEMOUSTOKES=WAM2NEMO%NEMOUSTOKES, NEMOVSTOKES=WAM2NEMO%NEMOVSTOKES, NEMOSTRN=WAM2NEMO%NEMOSTRN, &
&                            NPHIEPS=WAM2NEMO%NPHIEPS, NTAUOC=WAM2NEMO%NTAUOC, NSWH=WAM2NEMO%NSWH, NMWP=WAM2NEMO%NMWP, &
&                            NEMOTAUX=WAM2NEMO%NEMOTAUX, NEMOTAUY=WAM2NEMO%NEMOTAUY, NEMOWSWAVE=WAM2NEMO%NEMOWSWAVE, &
&                            NEMOPHIF=WAM2NEMO%NEMOPHIF)
    CALL INTFLDS_FIELD%INIT(WSEMEAN=INTFLDS%WSEMEAN, WSFMEAN=INTFLDS%WSFMEAN, USTOKES=INTFLDS%USTOKES, VSTOKES=INTFLDS%VSTOKES, &
&                           STRNMS=INTFLDS%STRNMS, TAUXD=INTFLDS%TAUXD, TAUYD=INTFLDS%TAUYD, TAUOCXD=INTFLDS%TAUOCXD, &
&                           TAUOCYD=INTFLDS%TAUOCYD, TAUOC=INTFLDS%TAUOC, PHIOCD=INTFLDS%PHIOCD, PHIEPS=INTFLDS%PHIEPS, &
&                           PHIAW=INTFLDS%PHIAW)
    CALL SRC_CONTRIBS%INIT(FL1=FL1, XLLWS=XLLWS, MIJ=MIJ)
    CALL nvtxEndRange

!$loki update_device

      CALL nvtxStartRange("IMPLSCH:UPDATE_DEVICE")
      CALL WVPRPT_FIELD%UPDATE_DEVICE(WAVNUM=WAVNUM_DPTR, CGROUP=CGROUP_DPTR, CIWA=CIWA_DPTR, CINV=CINV_DPTR, XK2CG=XK2CG_DPTR,  &
      & STOKFAC=STOKFAC_DPTR)
      CALL WVENVI_FIELD%UPDATE_DEVICE(EMAXDPT=EMAXDPT_DPTR, INDEP=INDEP_DPTR, DEPTH=DEPTH_DPTR, IOBND=IOBND_DPTR, IODP=IODP_DPTR)
      CALL FF_NOW_FIELD%UPDATE_DEVICE(AIRD=AIRD_DPTR, WDWAVE=WDWAVE_DPTR, CICOVER=CICOVER_DPTR, WSWAVE=WSWAVE_DPTR,  &
      & WSTAR=WSTAR_DPTR, UFRIC=UFRIC_DPTR, TAUW=TAUW_DPTR, TAUWDIR=TAUWDIR_DPTR, Z0M=Z0M_DPTR, Z0B=Z0B_DPTR,  &
      & CHRNCK=CHRNCK_DPTR, CITHICK=CITHICK_DPTR)
      CALL WAM2NEMO_FIELD%UPDATE_DEVICE(NEMOUSTOKES=NEMOUSTOKES_DPTR, NEMOVSTOKES=NEMOVSTOKES_DPTR, NEMOSTRN=NEMOSTRN_DPTR,  &
      & NPHIEPS=NPHIEPS_DPTR, NTAUOC=NTAUOC_DPTR, NSWH=NSWH_DPTR, NMWP=NMWP_DPTR, NEMOTAUX=NEMOTAUX_DPTR,  &
      & NEMOTAUY=NEMOTAUY_DPTR, NEMOWSWAVE=NEMOWSWAVE_DPTR, NEMOPHIF=NEMOPHIF_DPTR)
      CALL INTFLDS_FIELD%UPDATE_DEVICE(WSEMEAN=WSEMEAN_DPTR, WSFMEAN=WSFMEAN_DPTR, USTOKES=USTOKES_DPTR,  &
      & VSTOKES=VSTOKES_DPTR, STRNMS=STRNMS_DPTR, TAUXD=TAUXD_DPTR, TAUYD=TAUYD_DPTR, TAUOCXD=TAUOCXD_DPTR,  &
      & TAUOCYD=TAUOCYD_DPTR, TAUOC=TAUOC_DPTR, PHIOCD=PHIOCD_DPTR, PHIEPS=PHIEPS_DPTR, PHIAW=PHIAW_DPTR)
      CALL SRC_CONTRIBS%UPDATE_DEVICE(FL1=FL1_DPTR, XLLWS=XLLWS_DPTR, MIJ=MIJ_DPTR)
      !$acc data create(fmeanws, emean, fmean, f1mean, akmean, xkmean, phiwa, flm, coswdif, sinwdif2, rhowgdfth, fld, sl, spos, cireduc, ssource)
      CALL nvtxEndRange

      CALL nvtxStartRange("IMPLSCH")
!$loki data

      TIME0=-WAM_USER_CLOCK()

      DO ICHNK=1,NCHNK
        CALL IMPLSCH_FIRST_PART(1, NPROMA_WAM, FL1_DPTR(:,:,:,ICHNK), WAVNUM_DPTR(:,:,ICHNK), &
        & CGROUP_DPTR(:,:,ICHNK), CIWA_DPTR(:,:,ICHNK), &
        & EMAXDPT_DPTR(:,ICHNK), &
        & WDWAVE_DPTR(:,ICHNK),  &
        & CICOVER_DPTR(:,ICHNK), &
        & EMEAN(:,ICHNK), FMEAN(:,ICHNK), &
        & F1MEAN(:,ICHNK), AKMEAN(:,ICHNK), XKMEAN(:,ICHNK), &
        & FLM(:,:,ICHNK), &
        & COSWDIF(:,:,ICHNK), SINWDIF2(:,:,ICHNK), &
        & CIREDUC(:,:,:,ICHNK))
      END DO
      !IF (INONLIN_IMPL == 0) THEN
      !ELSE
        DO ICHNK=1,NCHNK
          NCALL = 2
          DO ICALL = 1, NCALL 
            LUPDTUS = .TRUE.
            CALL SINFLX (ICALL, NCALL, 1, NPROMA_WAM, LUPDTUS, &
            & FL1_DPTR(:,:,:,ICHNK), &
            & WAVNUM_DPTR(:,:,ICHNK), CINV_DPTR(:,:,ICHNK), XK2CG_DPTR(:,:,ICHNK), &
            & WSWAVE_DPTR(:,ICHNK), WDWAVE_DPTR(:,ICHNK), AIRD_DPTR(:,ICHNK), &
            & WSTAR_DPTR(:,ICHNK), CICOVER_DPTR(:,ICHNK), &
            & COSWDIF(:,:,ICHNK), SINWDIF2(:,:,ICHNK), &
            & FMEAN(:,ICHNK), HALP(:,ICHNK), FMEANWS(:,ICHNK), &
            & FLM(:,:,ICHNK), &
            & UFRIC_DPTR(:,ICHNK), TAUW_DPTR(:,ICHNK), TAUWDIR_DPTR(:,ICHNK), &
            & Z0M_DPTR(:,ICHNK), Z0B_DPTR(:,ICHNK), CHRNCK_DPTR(:,ICHNK), PHIWA(:,ICHNK), &
            & FLD(:,:,:,ICHNK), SL(:,:,:,ICHNK), SPOS(:,:,:,ICHNK), &
            & MIJ_DPTR(:,ICHNK), RHOWGDFTH(:,:,ICHNK), XLLWS_DPTR(:,:,:,ICHNK))
          ENDDO
        END DO
        DO ICHNK=1,NCHNK
          CALL IMPLSCH_SDISSIP(1, NPROMA_WAM, FL1_DPTR(:,:,:,ICHNK), WAVNUM_DPTR(:,:,ICHNK), &
          & CINV_DPTR(:,:,ICHNK), &
          & XK2CG_DPTR(:,:,ICHNK), &
          & INDEP_DPTR(:,ICHNK), &
          & AIRD_DPTR(:,ICHNK), WDWAVE_DPTR(:,ICHNK),  &
          & CICOVER_DPTR(:,ICHNK), WSWAVE_DPTR(:,ICHNK), WSTAR_DPTR(:,ICHNK), &
          & UFRIC_DPTR(:,ICHNK), TAUW_DPTR(:,ICHNK), TAUWDIR_DPTR(:,ICHNK), &
          & Z0M_DPTR(:,ICHNK), Z0B_DPTR(:,ICHNK), CHRNCK_DPTR(:,ICHNK), &
          & MIJ_DPTR(:,ICHNK), XLLWS_DPTR(:,:,:,ICHNK), &
          & FMEANWS(:,ICHNK), EMEAN(:,ICHNK), FMEAN(:,ICHNK), &
          & F1MEAN(:,ICHNK), XKMEAN(:,ICHNK), &
          & PHIWA(:,ICHNK), &
          & FLM(:,:,ICHNK), &
          & COSWDIF(:,:,ICHNK), SINWDIF2(:,:,ICHNK), &
          & RHOWGDFTH(:,:,ICHNK), &
          & FLD(:,:,:,ICHNK), SL(:,:,:,ICHNK), SPOS(:,:,:,ICHNK), &
          & SSOURCE(:,:,:,ICHNK))
        END DO
      !ENDIF
      IF (INONLIN_IMPL == 0) THEN
        DO ICHNK=1,NCHNK
          CALL IMPLSCH_SNONLIN(1, NPROMA_WAM, FL1_DPTR(:,:,:,ICHNK), &
          & FLD(:,:,:,ICHNK), SL(:,:,:,ICHNK), &
          & WAVNUM_DPTR(:,:,ICHNK), DEPTH_DPTR(:,ICHNK), AKMEAN(:,ICHNK))
        END DO
      ELSE
        CALL SNONLIN_CUDA(1, NPROMA_WAM, NCHNK, FL1_DPTR, FLD, SL, WAVNUM_DPTR, &
        & DEPTH_DPTR, AKMEAN)
      ENDIF
      DO ICHNK=1,NCHNK
        CALL IMPLSCH_AFTER_SNONLIN(1, NPROMA_WAM, FL1_DPTR(:,:,:,ICHNK), WAVNUM_DPTR(:,:,ICHNK), &
        & CINV_DPTR(:,:,ICHNK), &
        & XK2CG_DPTR(:,:,ICHNK), STOKFAC_DPTR(:,:,ICHNK), EMAXDPT_DPTR(:,ICHNK), &
        & DEPTH_DPTR(:,ICHNK), IOBND_DPTR(:,ICHNK), &
        & IODP_DPTR(:,ICHNK), AIRD_DPTR(:,ICHNK), WDWAVE_DPTR(:,ICHNK),  &
        & CICOVER_DPTR(:,ICHNK), WSWAVE_DPTR(:,ICHNK), &
        & UFRIC_DPTR(:,ICHNK), &
        & CITHICK_DPTR(:,ICHNK), NEMOUSTOKES_DPTR(:,ICHNK), NEMOVSTOKES_DPTR(:,ICHNK), &
        & NEMOSTRN_DPTR(:,ICHNK), NPHIEPS_DPTR(:,ICHNK), NTAUOC_DPTR(:,ICHNK), &
        & NSWH_DPTR(:,ICHNK), NMWP_DPTR(:,ICHNK), NEMOTAUX_DPTR(:,ICHNK), &
        & NEMOTAUY_DPTR(:,ICHNK), NEMOWSWAVE_DPTR(:,ICHNK), NEMOPHIF_DPTR(:,ICHNK), &
        & WSEMEAN_DPTR(:,ICHNK), WSFMEAN_DPTR(:,ICHNK), USTOKES_DPTR(:,ICHNK), &
        & VSTOKES_DPTR(:,ICHNK), STRNMS_DPTR(:,ICHNK), TAUXD_DPTR(:,ICHNK), &
        & TAUYD_DPTR(:,ICHNK), TAUOCXD_DPTR(:,ICHNK), TAUOCYD_DPTR(:,ICHNK), &
        & TAUOC_DPTR(:,ICHNK), PHIOCD_DPTR(:,ICHNK), PHIEPS_DPTR(:,ICHNK),  &
        & PHIAW_DPTR(:,ICHNK), MIJ_DPTR(:,ICHNK), XLLWS_DPTR(:,:,:,ICHNK), &
        & FMEANWS(:,ICHNK), EMEAN(:,ICHNK), FMEAN(:,ICHNK), &
        & F1MEAN(:,ICHNK), AKMEAN(:,ICHNK), XKMEAN(:,ICHNK), &
        & PHIWA(:,ICHNK), &
        & FLM(:,:,ICHNK), &
        & COSWDIF(:,:,ICHNK), &
        & RHOWGDFTH(:,:,ICHNK), &
        & FLD(:,:,:,ICHNK), SL(:,:,:,ICHNK), SPOS(:,:,:,ICHNK), &
        & CIREDUC(:,:,:,ICHNK), &
        & SSOURCE(:,:,:,ICHNK))
      END DO

      TIME_PHYS_KERNEL = TIME_PHYS_KERNEL + (TIME0+WAM_USER_CLOCK())*1.E-06

!$loki end data
      CALL nvtxEndRange

      CALL nvtxStartRange("IMPLSCH:ENSURE_HOST")
      !$acc end data
      CALL WVPRPT_FIELD%ENSURE_HOST()
      CALL WVENVI_FIELD%ENSURE_HOST()
      CALL FF_NOW_FIELD%ENSURE_HOST()
      CALL WAM2NEMO_FIELD%ENSURE_HOST()
      CALL INTFLDS_FIELD%ENSURE_HOST()
      CALL SRC_CONTRIBS%ENSURE_HOST()
      CALL nvtxEndRange

!$loki update_host
    CALL nvtxStartRange("IMPLSCH:FINAL")
    CALL WVPRPT_FIELD%FINAL()
    CALL WVENVI_FIELD%FINAL()
    CALL FF_NOW_FIELD%FINAL()
    CALL WAM2NEMO_FIELD%FINAL()
    CALL INTFLDS_FIELD%FINAL()
    CALL SRC_CONTRIBS%FINAL()
    CALL nvtxEndRange

    TIME_PHYS = TIME_PHYS + (TIME0+WAM_USER_CLOCK())*1.E-06

    IF (LWNEMOCOU) NEMONTAU = NEMONTAU + 1

  ELSE
!   NO SOURCE TERM CONTRIBUTION
!$OMP      PARALLEL DO SCHEDULE(STATIC) PRIVATE(ICHNK)  
    DO ICHNK = 1, NCHNK
      MIJ(:,ICHNK) = NFRE
      FL1(:,:,:,ICHNK) = MAX(FL1(:,:,:,ICHNK), EPSMIN)
      XLLWS(:,:,:,ICHNK) = 0.0_JWRB
    ENDDO
!$OMP      END PARALLEL DO
  ENDIF
  CALL GSTATS(1431,1)


!*       UPDATE FORCING FIELDS TIME COUNTER 
!        ----------------------------------
  IF (LLNEWFILE) THEN
    LLNEWFILE = .FALSE.
    IDELWH = MAX(IDELWI, IDELPRO)
    CALL INCDATE(CDAWIFL, IDELWH)
    CALL INCDATE(CDATEFL, IDELWH)
  ENDIF

  CDATEWO=CDATEWH
  CDTIMP=CDTIMPNEXT
  CALL INCDATE(CDTIMPNEXT, IDELT)   

ENDIF

IF (LHOOK) CALL DR_HOOK('WAMINTGR',1,ZHOOK_HANDLE)

END SUBROUTINE WAMINTGR_LOKI_GPU
