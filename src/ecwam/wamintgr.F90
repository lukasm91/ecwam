! (C) Copyright 1989- ECMWF.
!
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.
!

SUBROUTINE WAMINTGR (CDTPRA, CDATE, CDATEWH, CDTIMP, CDTIMPNEXT,  &
 &                   BLK2GLO,                                     &
 &                   WVENVI, WVPRPT, FF_NOW, FF_NEXT, INTFLDS,    &
 &                   WAM2NEMO, MIJ, FL1, XLLWS)


! ----------------------------------------------------------------------

!**** *WAMINTGR* - 3-G WAM MODEL - TIME INTEGRATION OF WAVE FIELDS.

!*    PURPOSE.
!     --------

!       COMPUTATION OF THE 2-D FREQUENCY-DIRECTION WAVE SPECTRUM AT ALL
!       GRID POINTS FOR A GIVEN INITIAL SPECTRUM AND FORCING SURFACE
!       STRESS FIELD.

!     REFERENCE.
!     ----------

!         IFS DOCUMENTATION, part VII

! -------------------------------------------------------------------

USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU
USE YOWDRVTYPE  , ONLY : WVGRIDGLO, ENVIRONMENT, FREQUENCY, FORCING_FIELDS,  &
 &                       INTGT_PARAM_FIELDS, WAVE2OCEAN

USE YOWCOUP  , ONLY : LWNEMOCOU, NEMONTAU
USE YOWGRID  , ONLY : NPROMA_WAM, NCHNK
USE YOWPARAM , ONLY : NIBLO, NANG, NFRE, NANG_PARAM
USE YOWINDN  , ONLY : MLSTHG
USE YOWPCONS , ONLY : EPSMIN
USE YOWSTAT  , ONLY : CDTPRO, IDELPRO, IDELT, IDELWI, LLSOURCE, TIME_PROPAG, &
 &                    TIME_PHYS
USE YOWWIND  , ONLY : CDAWIFL, CDATEWO, CDATEFL
USE YOWFIELD_MOD, ONLY : FREQUENCY_FIELD, ENVIRONMENT_FIELD, FORCING_FIELDS_FIELD, &
 &  WAVE2OCEAN_FIELD, INTGT_PARAM_FIELDS_FIELD, SOURCE_CONTRIBS_FIELD

USE YOMHOOK  , ONLY : LHOOK, DR_HOOK, JPHOOK
USE NVTX
      
! ----------------------------------------------------------------------

IMPLICIT NONE

#include "implsch.intfb.h"
#include "incdate.intfb.h"
#include "newwind.intfb.h"
#include "propag_wam.intfb.h"
#include "wam_user_clock.intfb.h"

CHARACTER(LEN=14), INTENT(IN)                                            :: CDTPRA  ! DATE FOR CALL PROPAGATION
CHARACTER(LEN=14), INTENT(INOUT)                                         :: CDATE  ! CURRENT DATE
CHARACTER(LEN=14), INTENT(INOUT)                                         :: CDATEWH ! DATE OF THE NEXT FORCING FIELDS
CHARACTER(LEN=14), INTENT(INOUT)                                         :: CDTIMP  ! START DATE OF SOURCE FUNCTION INTEGRATION
CHARACTER(LEN=14), INTENT(INOUT)                                         :: CDTIMPNEXT  ! NEXT START DATE OF SOURCE FUNCTION INTEGRATION
TYPE(WVGRIDGLO), INTENT(IN)                                              :: BLK2GLO  ! BLOCK TO GRID TRANSFORMATION
TYPE(ENVIRONMENT), INTENT(INOUT)                                         :: WVENVI !  WAVE ENVIRONMENT FIELDS
TYPE(FREQUENCY), INTENT(INOUT)                                           :: WVPRPT  ! WAVE PROPERTIES FIELDS
TYPE(FORCING_FIELDS), INTENT(INOUT)                                      :: FF_NOW  ! FORCING FIELDS AT CURRENT TIME
TYPE(FORCING_FIELDS), INTENT(IN)                                         :: FF_NEXT  !  DATA STRUCTURE WITH THE NEXT FORCING FIELDS
TYPE(INTGT_PARAM_FIELDS), INTENT(INOUT)                                  :: INTFLDS  ! INTEGRATED/DERIVED PARAMETERS
TYPE(WAVE2OCEAN), INTENT(INOUT)                                          :: WAM2NEMO  ! WAVE FIELDS PASSED TO NEMO
INTEGER(KIND=JWIM), DIMENSION(NPROMA_WAM, NCHNK), INTENT(INOUT)          :: MIJ  ! LAST FREQUENCY INDEX OF THE PROGNOSTIC RANGE
REAL(KIND=JWRB), DIMENSION(NPROMA_WAM, NANG, NFRE, NCHNK), INTENT(INOUT) :: FL1
REAL(KIND=JWRB), DIMENSION(NPROMA_WAM, NANG, NFRE, NCHNK), INTENT(INOUT) :: XLLWS ! TOTAL WINDSEA MASK FROM INPUT SOURCE TERM

REAL(KIND=JWRB) :: TIME0


INTEGER(KIND=JWIM) :: IJ, K, M
INTEGER(KIND=JWIM) :: ICHNK
INTEGER(KIND=JWIM) :: IDELWH

! Objects to store fields
TYPE(FREQUENCY_FIELD) :: WVPRPT_FIELD
TYPE(ENVIRONMENT_FIELD) :: WVENVI_FIELD
TYPE(FORCING_FIELDS_FIELD) :: FF_NOW_FIELD
TYPE(WAVE2OCEAN_FIELD) :: WAM2NEMO_FIELD
TYPE(INTGT_PARAM_FIELDS_FIELD) :: INTFLDS_FIELD
TYPE(SOURCE_CONTRIBS_FIELD) :: SRC_CONTRIBS

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE


LOGICAL, SAVE :: LLNEWFILE

DATA LLNEWFILE / .FALSE. /

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('WAMINTGR',0,ZHOOK_HANDLE)

!*     PROPAGATION TIME
!      ----------------

IF (CDATE == CDTPRA) THEN
  CALL nvtxStartRange("PROPAG")
  TIME0=-WAM_USER_CLOCK()
  CALL PROPAG_WAM(BLK2GLO, WVENVI, WVPRPT, FL1)
  TIME_PROPAG = TIME_PROPAG + (TIME0+WAM_USER_CLOCK())*1.E-06
  CDATE = CDTPRO
  cALL nvtxEndRange
ENDIF


!* RETRIEVING NEW FORCING FIELDS IF NEEDED.
!  ----------------------------------------
CALL nvtxStartRange("NEWWIND")
CALL NEWWIND(CDTIMP, CDATEWH, LLNEWFILE,      &
 &           WVPRPT, FF_NOW, FF_NEXT)
CALL nvtxEndRange

! IT IS TIME TO INTEGRATE THE SOURCE TERMS
! ----------------------------------------
IF (CDATE >= CDTIMPNEXT) THEN
! COMPUTE UPDATE DUE TO SOURCE TERMS
  CALL GSTATS(1431,0)
  IF (LLSOURCE) THEN

    TIME0=-WAM_USER_CLOCK()
    CALL nvtxStartRange("IMPLSCH:INIT")
    CALL WVPRPT_FIELD%INIT(WAVNUM=WVPRPT%WAVNUM, CGROUP=WVPRPT%CGROUP, CIWA=WVPRPT%CIWA, CINV=WVPRPT%CINV, XK2CG=WVPRPT%XK2CG, &
 &                         STOKFAC=WVPRPT%STOKFAC)
    CALL WVENVI_FIELD%INIT(EMAXDPT=WVENVI%EMAXDPT, INDEP=WVENVI%INDEP, DEPTH=WVENVI%DEPTH, IOBND=WVENVI%IOBND, IODP=WVENVI%IODP)
    CALL FF_NOW_FIELD%INIT(AIRD=FF_NOW%AIRD, WDWAVE=FF_NOW%WDWAVE, CICOVER=FF_NOW%CICOVER, WSWAVE=FF_NOW%WSWAVE, &
&                          WSTAR=FF_NOW%WSTAR, UFRIC=FF_NOW%UFRIC, TAUW=FF_NOW%TAUW, TAUWDIR=FF_NOW%TAUWDIR, &
&                          Z0M=FF_NOW%Z0M, Z0B=FF_NOW%Z0B, CHRNCK=FF_NOW%CHRNCK, CITHICK=FF_NOW%CITHICK)
    CALL WAM2NEMO_FIELD%INIT(NEMOUSTOKES=WAM2NEMO%NEMOUSTOKES, NEMOVSTOKES=WAM2NEMO%NEMOVSTOKES, NEMOSTRN=WAM2NEMO%NEMOSTRN, &
&                            NPHIEPS=WAM2NEMO%NPHIEPS, NTAUOC=WAM2NEMO%NTAUOC, NSWH=WAM2NEMO%NSWH, NMWP=WAM2NEMO%NMWP, &
&                            NEMOTAUX=WAM2NEMO%NEMOTAUX, NEMOTAUY=WAM2NEMO%NEMOTAUY, NEMOWSWAVE=WAM2NEMO%NEMOWSWAVE, &
&                            NEMOPHIF=WAM2NEMO%NEMOPHIF)
    CALL INTFLDS_FIELD%INIT(WSEMEAN=INTFLDS%WSEMEAN, WSFMEAN=INTFLDS%WSFMEAN, USTOKES=INTFLDS%USTOKES, VSTOKES=INTFLDS%VSTOKES, &
&                           STRNMS=INTFLDS%STRNMS, TAUXD=INTFLDS%TAUXD, TAUYD=INTFLDS%TAUYD, TAUOCXD=INTFLDS%TAUOCXD, &
&                           TAUOCYD=INTFLDS%TAUOCYD, TAUOC=INTFLDS%TAUOC, PHIOCD=INTFLDS%PHIOCD, PHIEPS=INTFLDS%PHIEPS, &
&                           PHIAW=INTFLDS%PHIAW)
    CALL SRC_CONTRIBS%INIT(FL1=FL1, XLLWS=XLLWS, MIJ=MIJ)
    CALL nvtxEndRange

      CALL nvtxStartRange("IMPLSCH")
!$OMP PARALLEL DO SCHEDULE(DYNAMIC,1) PRIVATE(ICHNK) FIRSTPRIVATE(WVPRPT_FIELD, WVENVI_FIELD, FF_NOW_FIELD, WAM2NEMO_FIELD, &
!$OMP INTFLDS_FIELD, SRC_CONTRIBS)
      DO ICHNK=1,NCHNK
        CALL WVPRPT_FIELD%UPDATE_VIEW(ICHNK)
        CALL WVENVI_FIELD%UPDATE_VIEW(ICHNK)
        CALL FF_NOW_FIELD%UPDATE_VIEW(ICHNK)
        CALL WAM2NEMO_FIELD%UPDATE_VIEW(ICHNK)
        CALL INTFLDS_FIELD%UPDATE_VIEW(ICHNK)
        CALL SRC_CONTRIBS%UPDATE_VIEW(ICHNK)
        
        CALL IMPLSCH(1, NPROMA_WAM, SRC_CONTRIBS%FL1, WVPRPT_FIELD%WAVNUM, WVPRPT_FIELD%CGROUP, WVPRPT_FIELD%CIWA,  &
        & WVPRPT_FIELD%CINV, WVPRPT_FIELD%XK2CG, WVPRPT_FIELD%STOKFAC, WVENVI_FIELD%EMAXDPT, WVENVI_FIELD%INDEP,  &
        & WVENVI_FIELD%DEPTH, WVENVI_FIELD%IOBND, WVENVI_FIELD%IODP, FF_NOW_FIELD%AIRD, FF_NOW_FIELD%WDWAVE,  &
        & FF_NOW_FIELD%CICOVER, FF_NOW_FIELD%WSWAVE, FF_NOW_FIELD%WSTAR, FF_NOW_FIELD%UFRIC, FF_NOW_FIELD%TAUW,  &
        & FF_NOW_FIELD%TAUWDIR, FF_NOW_FIELD%Z0M, FF_NOW_FIELD%Z0B, FF_NOW_FIELD%CHRNCK, FF_NOW_FIELD%CITHICK,  &
        & WAM2NEMO_FIELD%NEMOUSTOKES, WAM2NEMO_FIELD%NEMOVSTOKES, WAM2NEMO_FIELD%NEMOSTRN, WAM2NEMO_FIELD%NPHIEPS,  &
        & WAM2NEMO_FIELD%NTAUOC, WAM2NEMO_FIELD%NSWH, WAM2NEMO_FIELD%NMWP, WAM2NEMO_FIELD%NEMOTAUX, WAM2NEMO_FIELD%NEMOTAUY,  &
        & WAM2NEMO_FIELD%NEMOWSWAVE, WAM2NEMO_FIELD%NEMOPHIF, INTFLDS_FIELD%WSEMEAN, INTFLDS_FIELD%WSFMEAN,  &
        & INTFLDS_FIELD%USTOKES, INTFLDS_FIELD%VSTOKES, INTFLDS_FIELD%STRNMS, INTFLDS_FIELD%TAUXD, INTFLDS_FIELD%TAUYD,  &
        & INTFLDS_FIELD%TAUOCXD, INTFLDS_FIELD%TAUOCYD, INTFLDS_FIELD%TAUOC, INTFLDS_FIELD%PHIOCD, INTFLDS_FIELD%PHIEPS,  &
        & INTFLDS_FIELD%PHIAW, SRC_CONTRIBS%MIJ, SRC_CONTRIBS%XLLWS)
      END DO
!$OMP END PARALLEL DO
      CALL nvtxEndRange

    CALL nvtxStartRange("IMPLSCH:FINAL")
    CALL WVPRPT_FIELD%FINAL()
    CALL WVENVI_FIELD%FINAL()
    CALL FF_NOW_FIELD%FINAL()
    CALL WAM2NEMO_FIELD%FINAL()
    CALL INTFLDS_FIELD%FINAL()
    CALL SRC_CONTRIBS%FINAL()
    CALL nvtxEndRange

    TIME_PHYS = TIME_PHYS + (TIME0+WAM_USER_CLOCK())*1.E-06

    IF (LWNEMOCOU) NEMONTAU = NEMONTAU + 1

  ELSE
!   NO SOURCE TERM CONTRIBUTION
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(ICHNK)
    DO ICHNK = 1, NCHNK
      MIJ(:,ICHNK) = NFRE
      FL1(:,:,:,ICHNK) = MAX(FL1(:,:,:,ICHNK), EPSMIN)
      XLLWS(:,:,:,ICHNK) = 0.0_JWRB
    ENDDO
!$OMP END PARALLEL DO
  ENDIF
  CALL GSTATS(1431,1)


!*       UPDATE FORCING FIELDS TIME COUNTER
!        ----------------------------------
  IF (LLNEWFILE) THEN
    LLNEWFILE = .FALSE.
    IDELWH = MAX(IDELWI, IDELPRO)
    CALL INCDATE(CDAWIFL, IDELWH)
    CALL INCDATE(CDATEFL, IDELWH)
  ENDIF

  CDATEWO=CDATEWH
  CDTIMP=CDTIMPNEXT
  CALL INCDATE(CDTIMPNEXT, IDELT)

ENDIF

IF (LHOOK) CALL DR_HOOK('WAMINTGR',1,ZHOOK_HANDLE)

END SUBROUTINE WAMINTGR
