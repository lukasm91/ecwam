! (C) Copyright 1989- ECMWF.
! 
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.
!

SUBROUTINE SINFLX (ICALL, NCALL, KIJS, KIJL,                       &
 &                 LUPDTUS,                                        &
 &                 FL1,                                            &
 &                 WAVNUM, CINV, XK2CG,                            &
 &                 WSWAVE, WDWAVE, AIRD, WSTAR, CICOVER,    &
 &                 COSWDIF, SINWDIF2,                              &
 &                 FMEAN, HALP, FMEANWS,                           &
 &                 FLM,                                            &
 &                 UFRIC, TAUW, TAUWDIR, Z0M, Z0B, CHRNCK, PHIWA,  &
 &                 FLD, SL, SPOS,                                  &
 &                 MIJ, RHOWGDFTH, XLLWS)

! ----------------------------------------------------------------------

!**** *SINFLX* - UPDATE STRESS AND COMPUTE WIND INPUT SOURCE TERM. 

! ----------------------------------------------------------------------

      USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU

      USE YOWCOUP  , ONLY : LWCOU    ,LLCAPCHNK , LLGCBZ0, LLNORMAGAM
      USE YOWPARAM , ONLY : NANG     ,NFRE
      USE YOWPHYS  , ONLY : DTHRN_A  ,DTHRN_U 
      USE YOWWNDG  , ONLY : ICODE    ,ICODE_CPL
      USE YOWPCONS , ONLY : ROWATERM1 

      USE YOMHOOK  , ONLY : LHOOK    ,DR_HOOK, JPHOOK

! ----------------------------------------------------------------------

      IMPLICIT NONE

#include "airsea.intfb.h"
#include "femeanws.intfb.h"
#include "frcutindex.intfb.h"
#include "halphap.intfb.h"
#include "sinput_ard.intfb.h"
#include "stresso.intfb.h"

INTEGER(KIND=JWIM), INTENT(IN) :: ICALL  !! CALL NUMBER.
INTEGER(KIND=JWIM), INTENT(IN) :: NCALL  !! TOTAL NUMBER OF CALLS.
INTEGER(KIND=JWIM), INTENT(IN) :: KIJS, KIJL !! GRID POINT INDEXES.  

LOGICAL, INTENT(IN) :: LUPDTUS  !! IF TRUE UFRIC AND Z0M WILL BE UPDATED (CALLING AIRSEA).

REAL(KIND=JWRB), DIMENSION(KIJL,NANG,NFRE), INTENT(INOUT) :: FL1  !! WAVE SPECTRUM.
REAL(KIND=JWRB), DIMENSION(KIJL,NFRE), INTENT(IN) :: WAVNUM  !! WAVE NUMBER.
REAL(KIND=JWRB), DIMENSION(KIJL,NFRE), INTENT(IN) :: CINV    !! INVERSE PHASE VELOCITY.
REAL(KIND=JWRB), DIMENSION(KIJL,NFRE), INTENT(IN) :: XK2CG  !! (WAVNUM)**2 * GROUP SPPED.
REAL(KIND=JWRB), DIMENSION(KIJL), INTENT(INOUT) :: WSWAVE !! WIND SPEED IN M/S.
REAL(KIND=JWRB), DIMENSION(KIJL), INTENT(IN) :: WDWAVE !! WIND DIRECTION IN RADIANS IN OCEANOGRAPHIC NOTATION.
REAL(KIND=JWRB), DIMENSION(KIJL), INTENT(IN) :: AIRD  !! AIR DENSITY (KG/M**3).
REAL(KIND=JWRB), DIMENSION(KIJL), INTENT(IN) :: WSTAR !! FREE CONVECTION VELOCITY SCALE (M/S)
REAL(KIND=JWRB), DIMENSION(KIJL), INTENT(IN) :: CICOVER  !! SEA ICE COVER.
REAL(KIND=JWRB), DIMENSION(KIJL, NANG), INTENT(IN) :: COSWDIF  !! COS(TH(K)-WDWAVE(IJ))
REAL(KIND=JWRB), DIMENSION(KIJL, NANG), INTENT(IN) :: SINWDIF2 !! SIN(TH(K)-WDWAVE(IJ))**2
REAL(KIND=JWRB), DIMENSION(KIJL), INTENT(IN) :: FMEAN  !! MEAN FREQUENCY.
REAL(KIND=JWRB), DIMENSION(KIJL), INTENT(INOUT) :: HALP   !! 1/2 PHILLIPS PARAMETER  
REAL(KIND=JWRB), DIMENSION(KIJL), INTENT(OUT) :: FMEANWS  !! MEAN FREQUENCY OF THE WINDSEA.
REAL(KIND=JWRB), DIMENSION(KIJL,NANG), INTENT(IN) :: FLM  !! SPECTAL DENSITY MINIMUM VALUE
REAL(KIND=JWRB), DIMENSION(KIJL), INTENT(INOUT) :: UFRIC !! FRICTION VELOCITY IN M/S.
REAL(KIND=JWRB), DIMENSION(KIJL), INTENT(INOUT) :: TAUW  !! WAVE STRESS IN (M/S)**2
REAL(KIND=JWRB), DIMENSION(KIJL), INTENT(INOUT) :: TAUWDIR  !! WAVE STRESS DIRECTION.
REAL(KIND=JWRB), DIMENSION(KIJL), INTENT(INOUT) :: Z0M  !! ROUGHNESS LENGTH IN M.
REAL(KIND=JWRB), DIMENSION(KIJL), INTENT(INOUT) :: Z0B  !! BACKGROUND ROUGHNESS LENGTH.
REAL(KIND=JWRB), DIMENSION(KIJL), INTENT(INOUT) :: CHRNCK  !! CHARNOCK COEFFICIENT.

REAL(KIND=JWRB), DIMENSION(KIJL), INTENT(OUT) :: PHIWA  !! ENERGY FLUX FROM WIND INTO WAVES.
REAL(KIND=JWRB), DIMENSION(KIJL,NANG,NFRE), INTENT(OUT) :: FLD !! DIAGONAL MATRIX OF FUNCTIONAL DERIVATIVE.
REAL(KIND=JWRB), DIMENSION(KIJL,NANG,NFRE), INTENT(OUT) :: SL !! TOTAL SOURCE FUNCTION ARRAY.
REAL(KIND=JWRB), DIMENSION(KIJL,NANG,NFRE), INTENT(OUT) :: SPOS !!  POSITIVE SINPUT ONLY.

INTEGER(KIND=JWIM), INTENT(OUT) :: MIJ(KIJL)  !! LAST FREQUENCY INDEX OF THE PROGNOSTIC RANGE.

REAL(KIND=JWRB), DIMENSION(KIJL,NFRE), INTENT(OUT) :: RHOWGDFTH  !! WATER DENSITY * G * DF * DTHETA

REAL(KIND=JWRB), DIMENSION(KIJL,NANG,NFRE), INTENT(OUT) :: XLLWS  !! TOTAL WINDSEA MASK FROM INPUT SOURCE TERM.


INTEGER(KIND=JWIM) :: IJ, K
INTEGER(KIND=JWIM) :: IUSFG, ICODE_WND
INTEGER(KIND=JWIM) :: NGST

REAL(KIND=JWRB), DIMENSION(KIJL,NANG) :: HALPHAP_WD
REAL(KIND=JWRB), DIMENSION(KIJL,NANG,NFRE) :: HALPHAP_FLWD
REAL(KIND=JWRB), DIMENSION(KIJL,NANG,2) :: TMP_GAM0, TMP_DSTAB

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE
REAL(KIND=JWRB) :: RNFAC
REAL(KIND=JWRB) :: RAORW  !! RATIO AIR DENSITY TO WATER DENSITY.

LOGICAL :: LLPHIWA, LLSNEG

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SINFLX',0,ZHOOK_HANDLE)

DO IJ=KIJS,KIJL
  RAORW = MAX(AIRD(IJ), 1.0_JWRB) * ROWATERM1

! UPDATE UFRIC AND Z0M
  IF (ICALL == 1 ) THEN
    IUSFG = 0
    IF (LWCOU) THEN
      ICODE_WND = ICODE_CPL
    ELSE
      ICODE_WND = ICODE
    ENDIF
  ELSE
    IUSFG = 1
    ICODE_WND = 3
  ENDIF

  IF(LLNORMAGAM .AND. LLCAPCHNK ) THEN
    RNFAC = 1.0_JWRB+DTHRN_A*(1.0_JWRB+TANH(WSWAVE(IJ)-DTHRN_U))
  ELSE
    RNFAC = 1.0_JWRB
  ENDIF


  IF(LUPDTUS) THEN
  ! increase noise level in the tail
    IF (ICALL == 1 ) THEN
      DO K=1,NANG
      FL1(IJ,K,NFRE) = MAX(FL1(IJ,K,NFRE),FLM(IJ,K))
      ENDDO

      IF (LLGCBZ0) THEN
        CALL HALPHAP_PW(IJ, KIJS, KIJL, WAVNUM, COSWDIF, FL1, HALP(IJ), HALPHAP_WD, HALPHAP_FLWD)
      ELSE
        HALP(IJ) = 0.0_JWRB 
      ENDIF
    ENDIF

    CALL AIRSEA (HALP(IJ), WSWAVE(IJ), WDWAVE(IJ), TAUW(IJ), TAUWDIR(IJ), RNFAC,  &
&                UFRIC(IJ), Z0M(IJ), Z0B(IJ), CHRNCK(IJ), ICODE_WND, IUSFG) 
  ENDIF

  ! COMPUTE WIND INPUT
  !!  FLD AND SL ARE INITIALISED IN SINPUT
  IF(ICALL < NCALL ) THEN
    NGST = 1
    LLPHIWA = .FALSE.
    LLSNEG = .FALSE.
  ELSE
    NGST = 2
    LLPHIWA = .TRUE.
    LLSNEG = .TRUE.
  ENDIF

  CALL SINPUT_ARD (IJ, NGST, LLSNEG, KIJS, KIJL, FL1, &
  &            WAVNUM, CINV, XK2CG,           &
  &            WDWAVE(IJ), WSWAVE(IJ), UFRIC(IJ), Z0M(IJ),    &
  &            COSWDIF, SINWDIF2,             &
  &            RAORW, WSTAR(IJ), RNFAC,           &
  &            FLD, SL, SPOS, XLLWS, TMP_GAM0, TMP_DSTAB) 


! MEAN FREQUENCY CHARACTERISTIC FOR WIND SEA
  CALL FEMEANWS_PW(IJ, KIJL, FL1, XLLWS, FMEANWS(IJ))

! COMPUTE LAST FREQUENCY INDEX OF PROGNOSTIC PART OF SPECTRUM.
  CALL FRCUTINDEX_PW(IJ, KIJL, FMEAN(IJ), FMEANWS(IJ), UFRIC(IJ), CICOVER(IJ), MIJ(IJ), RHOWGDFTH)

! UPDATE TAUW
  CALL STRESSO_PW (IJ, KIJS, KIJL, MIJ(IJ), RHOWGDFTH,          &
  &             FL1, SL, SPOS,                       &
  &             CINV,                                &
  &             WDWAVE(IJ), UFRIC(IJ), Z0M(IJ), AIRD(IJ), RNFAC,     &
  &             COSWDIF, SINWDIF2,                   &
  &             TAUW(IJ), TAUWDIR(IJ), PHIWA(IJ), LLPHIWA) 
ENDDO

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SINFLX',1,ZHOOK_HANDLE)

END SUBROUTINE SINFLX
